#version 450
#extension GL_ARB_separate_shader_objects : enable

layout(local_size_x = 16, local_size_y = 16) in;

layout(binding = 0) uniform samplerCube envMap;
layout(binding = 1, rgba32f) writeonly uniform imageCube prefilteredMap;

const float PI = 3.1415926535897932384626433832795;

vec3 CubeCoordinateToWorld(ivec3 cubeCoordinate, vec2 cubemapSize);
ivec3 TextureCoordinateToCube(vec3 textureCoordinate, vec2 cubemapSize);
float Random(vec2 co);
vec3 PrefilterEnvMap(vec3 R, float roughness);
float RadicalInverse_VdC(uint bits);
vec2 Hammersley2D(uint i, uint N);
vec3 ImportanceSample_GGX(vec2 Xi, vec3 normal, float roughness);
float D_GGX(float NdotH, float roughness);

layout(push_constant) uniform PushConstants
{
	vec2 cubemapSize;
	float roughness;
	int samplesCount;
};

void main()
{
	ivec3 cubeCoordinate = ivec3(gl_GlobalInvocationID);
	vec3 positionWS = CubeCoordinateToWorld(cubeCoordinate, cubemapSize);

	vec3 color = PrefilterEnvMap(normalize(positionWS), roughness);

	imageStore(prefilteredMap, cubeCoordinate, vec4(color, 1.0));
}

vec3 PrefilterEnvMap(vec3 R, float roughness)
{
	vec3 N = R;
	vec3 V = R;

	vec3 color = vec3(0.0);
	float totalWeight = 0.0;
	float envMapSize = float(textureSize(envMap, 0).s);

	for(uint i = 0u; i < samplesCount; ++i)
	{
		vec2 Xi = Hammersley2D(i, samplesCount);
		vec3 H = ImportanceSample_GGX(Xi, N, roughness);
		vec3 L = 2.0 *  dot(V, H) * H - V;
		float NdotL = clamp(dot(N, L), 0.0, 1.0);

		if (NdotL > 0.0)
		{
			float NdotH = clamp(dot(N, H), 0.0, 1.0);
			float VdotH = clamp(dot(V, H), 0.0, 1.0);

			float pdf = D_GGX(NdotH, roughness) * NdotH / (4.0 / VdotH) + 0.0001;

			float omegaS = 1.0 / (float(samplesCount) * pdf);
			float omegaP = 4.0 * PI / (6.0 * envMapSize * envMapSize);
			float mipLevel = roughness == 0.0 ? 0.0 : max(0.5 * log2(omegaS / omegaP) + 1.0, 0.0);

			color += textureLod(envMap, L, mipLevel).rgb * NdotL;
			totalWeight += NdotL;
		}
	}

	return (color / totalWeight);
}

vec3 CubeCoordinateToWorld(ivec3 cubeCoordinate, vec2 cubemapSize)
{
	vec2 textureCoordinate = vec2(cubeCoordinate.xy) / cubemapSize;
	textureCoordinate = textureCoordinate * 2.0 - 1.0;

	switch(cubeCoordinate.z)
	{
		case 0:
			return vec3(1.0, -textureCoordinate.yx);
		case 1:
			return vec3(-1.0, -textureCoordinate.y, textureCoordinate.x);
		case 2:
			return vec3(textureCoordinate.x, 1.0, textureCoordinate.y);
		case 3:
			return vec3(textureCoordinate.x, -1.0, -textureCoordinate.y);
		case 4:
			return vec3(textureCoordinate.x, -textureCoordinate.y, 1.0);
		case 5:
			return vec3(-textureCoordinate.xy, -1.0);

	}

	return vec3(0.0);
}

ivec3 TextureCoordinateToCube(vec3 textureCoordinate, vec2 cubemapSize)
{
	vec3 absTexture = abs(textureCoordinate);
	textureCoordinate /= max(max(absTexture.x, absTexture.y), absTexture.z);

	float cubeFace;
	vec2 uvCoordinate;

	if (absTexture.x > absTexture.y && absTexture.x > absTexture.z)
	{
		float negativeX = step(textureCoordinate.x, 0.0);
		uvCoordinate = mix(-textureCoordinate.zy, vec2(textureCoordinate.z, -textureCoordinate.y), negativeX);
		cubeFace = negativeX;
	}
	else if (absTexture.y > absTexture.z)
	{
		float negativeY = step(textureCoordinate.y, 0.0);
		uvCoordinate = mix(textureCoordinate.xz, vec2(textureCoordinate.x, -textureCoordinate.z), negativeY);
		cubeFace = 2.0 + negativeY;
	}
	else
	{
		float negativeZ = step(textureCoordinate.z, 0.0);
		uvCoordinate = mix(vec2(textureCoordinate.x, -textureCoordinate.y), -textureCoordinate.xy, negativeZ);
		cubeFace = 4.0 + negativeZ;
	}

	uvCoordinate = (uvCoordinate + 1.0) * 0.5;
	uvCoordinate = uvCoordinate * cubemapSize;
	uvCoordinate = clamp(uvCoordinate, vec2(0.0), cubemapSize - vec2(1.0));

	return ivec3(ivec2(uvCoordinate), int(cubeFace));
}

float Random(vec2 co)
{
	float a = 12.9898;
	float b = 78.233;
	float c = 43758.5453;
	float dt= dot(co.xy ,vec2(a,b));
	float sn= mod(dt,3.14);
	return fract(sin(sn) * c);
}

float RadicalInverse_VdC(uint bits) 
{
     bits = (bits << 16u) | (bits >> 16u);
     bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
     bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
     bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
     bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
     return float(bits) * 2.3283064365386963e-10; // / 0x100000000
}


vec2 Hammersley2D(uint i, uint N)
{
	return vec2(float(i)/float(N), RadicalInverse_VdC(i));
}

vec3 ImportanceSample_GGX(vec2 Xi, vec3 normal, float roughness)
{
	float a = roughness * roughness;

	float phi = 2.0 * PI * Xi.x + Random(normal.xz) * 0.1;
	float cosTheta = sqrt((1.0 - Xi.y) / (1.0 + (a * a - 1.0) * Xi.y));
	float sinTheta = sqrt(1.0 - cosTheta * cosTheta);

	vec3 H = vec3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);

	vec3 up = abs(normal.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
	vec3 tangent = normalize(cross(up, normal));
	vec3 bitangent = normalize(cross(normal, tangent));

	return normalize(tangent * H.x + bitangent * H.y + normal * H.z);
}

float D_GGX(float NdotH, float roughness)
{
	float a = roughness * roughness;
	float a2 = a * a;
	
	float denom = NdotH * NdotH * (a2 - 1.0) + 1.0;

	return a2 / (PI * denom * denom);
}